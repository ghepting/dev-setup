#!/usr/bin/env zsh

# Check if we are running in zsh
[ -z "$ZSH_VERSION" ] && { log_error "Error: This script must be run in zsh."; exit 1; }

# source variables and utility functions
source "./lib/core/vars.sh"
source "./lib/core/utils.sh"

for lib in ./lib/modules/*.sh; do
  source "$lib"
done

log_info "Detected platform: ${PLATFORM}"

# create config directory if it doesn't exist
if [ ! -d "$CONFIG_DIR" ]
then
  mkdir -p "$CONFIG_DIR"
fi

# copy config file to ~/.config/dev-setup.conf if it doesn't exist
if [ ! -f "$CONFIG_FILE" ]
then
  cp "./dev-setup.conf" "$CONFIG_FILE"
  log_success "Created default configuration at $CONFIG_FILE"
fi

prompt_config() {
  log_info "--- Configuration Review ---"
  log_info "The setup will proceed with defaults based on your platform ($PLATFORM)."
  log_info "Config path: $CONFIG_FILE"

  log_info "Current Toggles:"
  local modules=(
    "dotfiles"
    "vim_tmux"
    "editor"
    "1password"
    "op_cli"
    "1password_ssh"
    "ruby"
    "python"
    "node"
    "docker"
    "app_store"
    "claude_code_cli"
    "gemini_cli"
    "postman_cli"
    "yubikey"
  )

  for module in "${modules[@]}"; do
    local module_status="${RED}disabled${NC}"
    is_enabled "$module" && module_status="${GREEN}enabled${NC}"
    printf "  %-20s : %b\n" "$module" "$module_status"
  done

  if confirm_action "\n${YELLOW}Proceed with current configuration?${NC}" "y"; then
    log_info "Using current configuration."
    log_info "----------------------------"
    return
  fi

  log_info "Interactive Configuration (press Enter to keep default):"
  for module in "${modules[@]}"; do
    local prompt_default="n"
    is_enabled "$module" && prompt_default="y"

    if confirm_action "  Enable ${CYAN}${module}${NC}?" "$prompt_default"; then
        set_config_value "$module" "true"
        if [[ "$module" == "dotfiles" ]]; then
          local current_repo current_dir
          current_repo=$(grep "^dotfiles_repo=" "$CONFIG_FILE" | cut -d= -f2 | tr -d '"')
          current_repo=${current_repo:-$DOTFILES_REPO}

          REPO_REPLY=$(prompt_input "    Repository URL" "$current_repo")
          set_config_value "dotfiles_repo" "$REPO_REPLY"

          current_dir=$(grep "^dotfiles_dir=" "$CONFIG_FILE" | cut -d= -f2 | tr -d '"')
          current_dir=${current_dir:-$DOTFILES_DIR}

          DIR_REPLY=$(prompt_input "    Directory path" "$current_dir")
          # Resolve ~ if present
          DIR_REPLY="${DIR_REPLY/#\~/$HOME}"
          set_config_value "dotfiles_dir" "$DIR_REPLY"
        fi
    else
        set_config_value "$module" "false"
    fi
  done
  log_success "Configuration updated."
  log_info "----------------------------"
}

# Handle execution
if [[ "${(%):-%N}" == "$0" ]]; then
  # Run the interactive configuration
  prompt_config

  # configuration toggles
  is_enabled "dotfiles" && setup_dotfiles && symlink_antigravity_config

  if is_macos; then
    # homebrew
    install_homebrew && install_homebrew_path && install_homebrew_formulae && setup_iterm_colors

    # app store apps
    is_enabled "app_store" && install_mas_apps

    # yubikey pam setup
    is_enabled "yubikey" && setup_yubikey_pam
  fi

  # configure Antigravity as default editor
  is_enabled "editor" && configure_editor && install_antigravity_extensions && update_antigravity_extensions_list

  # docker
  is_enabled "docker" && setup_docker

  # ruby and rbenv
  is_enabled "ruby" && install_rbenv_and_ruby

  # python and pyenv
  is_enabled "python" && install_pyenv_and_python

  # node/npm, and nvm
  is_enabled "node" && install_nvm_and_node

  # vim & tmux
  is_enabled "vim_tmux" && setup_vim_tmux_config

  # 1password modules
  is_enabled "1password" && setup_1password
  is_enabled "op_cli" && setup_1password_cli
  is_enabled "1password_ssh" && setup_1password_ssh

  # gemini CLI
  is_enabled "gemini_cli" && install_gemini_cli

  # claude code CLI
  is_enabled "claude_code_cli" && install_claude_code_cli

  # postman CLI
  is_enabled "postman_cli" && install_postman_cli

  # setup git hooks
  if [ -d ".git" ]; then
    if [[ "$(git config core.hooksPath)" != ".hooks" ]]; then
      log_info "Configuring git hooks..."
      git config core.hooksPath .hooks
      log_success "Successfully configured core.hooksPath to .hooks"
    fi
  fi

  log_success "Dev setup completed successfully"
  if $RESTART_REQUIRED
  then
    log_warn "You may need to restart your terminal"
  fi
  exit 0
fi
