#!/usr/bin/env zsh

# Check if we are running in zsh
[ -z "$ZSH_VERSION" ] && { echo "Error: This script must be run in zsh."; exit 1; }

# source variables and utility functions
source "./lib/core/vars.sh"
source "./lib/core/utils.sh"

for lib in ./lib/modules/*.sh; do
  source "$lib"
done

echo -e "${WHITE}Detected platform: ${PLATFORM}${NC}"

# create config directory if it doesn't exist
if [ ! -d "$CONFIG_DIR" ]
then
  mkdir -p "$CONFIG_DIR"
fi

# copy config file to ~/.config/dev-setup.conf if it doesn't exist
if [ ! -f "$CONFIG_FILE" ]
then
  cp "./dev-setup.conf" "$CONFIG_FILE"
  echo -e "${GREEN}Created default configuration at $CONFIG_FILE${NC}"
fi

prompt_config() {
  echo -e "\n${WHITE}--- Configuration Review ---${NC}"
  echo -e "The setup will proceed with defaults based on your platform ($PLATFORM)."
  echo -e "Config path: ${WHITE}$CONFIG_FILE${NC}"

  echo -e "\n${WHITE}Current Toggles:${NC}"
  local modules=(
    "google_drive"
    "dotfiles"
    "vim_tmux"
    "editor"
    "1password"
    "op_cli"
    "1password_ssh"
    "ruby"
    "python"
    "node"
    "docker"
    "app_store"
    "claude_code_cli"
    "gemini_cli"
    "postman_cli"
  )

  for module in "${modules[@]}"; do
    local module_status="${RED}disabled${NC}"
    is_enabled "$module" && module_status="${GREEN}enabled${NC}"
    printf "  %-20s : %b\n" "$module" "$module_status"
  done

  echo -n -e "\n${YELLOW}Proceed with current configuration? [Y/n]: ${NC}"
  read -r REPLY
  REPLY=${REPLY:-y}
  if [[ $REPLY =~ ^[Yy]$ ]]; then
    echo -e "${GREEN}Using current configuration.${NC}"
    echo -e "${YELLOW}----------------------------${NC}\n"
    return
  fi

  echo -e "\n${WHITE}Interactive Configuration (press Enter to keep default):${NC}"
  for module in "${modules[@]}"; do
    local current="n"
    is_enabled "$module" && current="y"

    echo -n -e "  Enable ${CYAN}${module}${NC}? [y/n] (default: $current): "
    read -r REPLY
    case "$REPLY" in
      [Yy]*)
        set_config_value "$module" "true"
        if [[ "$module" == "dotfiles" ]]; then
          local current_repo current_dir
          current_repo=$(grep "^dotfiles_repo=" "$CONFIG_FILE" | cut -d= -f2 | tr -d '"')
          current_repo=${current_repo:-$DOTFILES_REPO}
          echo -n -e "    Repository URL (default: $current_repo): "
          read -r REPO_REPLY
          REPO_REPLY=${REPO_REPLY:-$current_repo}
          set_config_value "dotfiles_repo" "$REPO_REPLY"

          current_dir=$(grep "^dotfiles_dir=" "$CONFIG_FILE" | cut -d= -f2 | tr -d '"')
          current_dir=${current_dir:-$DOTFILES_DIR}
          echo -n -e "    Directory path (default: $current_dir): "
          read -r DIR_REPLY
          DIR_REPLY=${DIR_REPLY:-$current_dir}
          # Resolve ~ if present
          DIR_REPLY="${DIR_REPLY/#\~/$HOME}"
          set_config_value "dotfiles_dir" "$DIR_REPLY"
        fi
        ;;
      [Nn]*)
        set_config_value "$module" "false"
        ;;
    esac
  done
  echo -e "\n${GREEN}Configuration updated.${NC}"
  echo -e "${YELLOW}----------------------------${NC}\n"
}

# Handle execution
if [[ "${(%):-%N}" == "$0" ]]; then
  # Run the interactive configuration
  prompt_config

  # initial setup
  if is_macos; then
    # install homebrew
    setup_homebrew
  fi

  # google drive setup
  setup_google_drive

  # configuration toggles
  if is_enabled "dotfiles"; then
    setup_dotfiles
    symlink_antigravity_config
  fi

  if is_macos; then
    # add homebrew to path
    install_homebrew_path

    # add ghostty to path
    install_ghostty_path

    # homebrew and formulae
    install_homebrew_formulae

    # iTerm2 colors
    setup_iterm_colors
  fi

  # configure Antigravity as default editor
  if is_enabled "editor"; then
    configure_editor
    install_antigravity_extensions
    update_antigravity_extensions_list
  fi

  # docker
  if is_enabled "docker"; then
    setup_docker
  fi

  # ruby and rbenv
  if is_enabled "ruby"; then
    install_rbenv_and_ruby
  fi

  # python and pyenv
  if is_enabled "python"; then
    install_pyenv_and_python
  fi

  # node/npm, and nvm
  if is_enabled "node"; then
    install_nvm_and_node
  fi

  # vim & tmux
  if is_enabled "vim_tmux"; then
    setup_vim_tmux_config
  fi

  # 1password modules
  if is_enabled "1password"; then
    setup_1password
  fi
  if is_enabled "op_cli"; then
    setup_1password_cli
  fi
  if is_enabled "1password_ssh"; then
    setup_1password_ssh
  fi

  # gemini CLI
  is_enabled "gemini_cli" && install_gemini_cli

  # claude
  is_enabled "claude_code_cli" && install_claude_code_cli

  # postman CLI
  is_enabled "postman_cli" && install_postman_cli

  # app store apps
  if is_macos && is_enabled "app_store"; then
    install_mas_apps
  fi

  # setup git hooks
  if [ -d ".git" ]; then
    if [[ "$(git config core.hooksPath)" != ".hooks" ]]; then
      echo -e "${WHITE}Configuring git hooks...${NC}"
      git config core.hooksPath .hooks
      echo -e "${GREEN}Successfully configured core.hooksPath to .hooks/${NC}"
    fi
  fi

  echo -e "\n${GREEN}Dev setup completed successfully${NC}"
  if $RESTART_REQUIRED
  then
    echo -e "${YELLOW}You may need to restart your terminal${NC}"
  fi
  exit 0
fi
